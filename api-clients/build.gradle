buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.openapitools:openapi-generator-gradle-plugin:3.3.4"
        classpath 'com.google.code.gson:gson:2.8.5'
    }
}

plugins {
    id "java-library"
    id 'kotlin'
    id 'de.fuerstenau.buildconfig' version '1.1.8'
}

apply plugin: 'org.openapi.generator'

group = 'tk.jonathancowling'
version = '0.0.1'

repositories {
    jcenter()
    google()
}

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains:annotations:17.0.0"
    compile "com.squareup.retrofit2:retrofit:$rootProject.ext.retrofit_version"
    compile "com.squareup.retrofit2:converter-scalars:$rootProject.ext.retrofit_version"
    compile "com.squareup.retrofit2:converter-gson:$rootProject.ext.retrofit_version"
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    compile "io.reactivex.rxjava2:rxjava:$rootProject.ext.rx_java_version"
    compile "io.swagger:swagger-annotations:$rootProject.ext.swagger_annotations_version"
    compile ("org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:$rootProject.ext.oltu_version"){
        exclude group:'org.apache.oltu.oauth2' , module: 'org.apache.oltu.oauth2.common'
    }
    compile 'org.yaml:snakeyaml:1.24'
    compile "io.gsonfire:gson-fire:$rootProject.ext.json_fire_version"
    compile "org.threeten:threetenbp:$rootProject.ext.threetenbp_version"
    testCompile "junit:junit:$rootProject.ext.junit_version"
}

buildConfig {

    buildConfigField 'String', 'API_KEY_NAME', "ApiKey"
}

openApiGenerate {
    generatorName = "java"
    outputDir = "${projectDir}"
    inputSpec = "${projectDir}/api/api.yml"
    configOptions = [
            sourceFolder: "build/generated/source/api/main/java",
            useRxJava2: true,
            java8: false,
            dateLibrary: "threetenbp",
            booleanGetterPrefix: "is"
    ]
    systemProperties = [
            modelDocs: "false",
            models: "",
            apis: "",
            supportingFiles: ""
    ]

    library = "retrofit2"
    modelPackage = "tk.jonathancowling.inventorytracker.clients.list.models"
    apiPackage = "tk.jonathancowling.inventorytracker.clients.list.api"
    invokerPackage = "tk.jonathancowling.inventorytracker.clients.list"
    groupId = "tk.jonathancowling"
    id = "inventorytracker-listclient"
    importMappings = [
            Item: "tk.jonathancowling.inventorytracker.clients.list.models.Item",
            ItemPrototype: "tk.jonathancowling.inventorytracker.clients.list.models.ItemPrototype",
            PartialItem: "tk.jonathancowling.inventorytracker.clients.list.models.PartialItem",
            NoApiKey: "tk.jonathancowling.inventorytracker.clients.list.models.NoApiKey"
    ]
    version = "0.0.1"
}

task compileGeneratedJava {
    dependsOn(tasks.openApiGenerate)
    sourceSets.all { sourceSet ->
        sourceSet.java.srcDirs += "build/generated/source/api/main/java"
        sourceSet.java.srcDirs += "build/generated/source/api/${sourceSet.name}/java"
    }
    // fixed bug in openapi plugin (bug raised for maven https://github.com/OpenAPITools/openapi-generator/issues/2737)
    project.delete(files("${projectDir}/gradle"))
}

// tasks.compileGenerated.dependsOn tasks.openApiGenerate
compileJava.dependsOn tasks.compileGeneratedJava
compileKotlin.dependsOn tasks.compileGeneratedJava